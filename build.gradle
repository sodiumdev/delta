plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version '8.1.1'
    id "io.papermc.paperweight.userdev" version "1.5.11"
    id "xyz.jpenilla.run-paper" version "2.2.2"
}

group = 'zip.sodium'
version = '0.01'

dependencies {
    paperweight.paperDevBundle("1.20.4-R0.1-SNAPSHOT")

    implementation 'org.ow2.asm:asm:9.6'
    implementation 'org.ow2.asm:asm-util:9.6'

    implementation 'org.reflections:reflections:0.10.2'
    implementation "io.github.karlatemp:unsafe-accessor:1.7.0"
}

jar {
    manifest {
        attributes(
                "Agent-Class": "zip.sodium.delta.agent.DeltaAgent",
                "Can-Retransform-Classes": true,
                "Can-Redefine-Classes": true
        )
    }
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

